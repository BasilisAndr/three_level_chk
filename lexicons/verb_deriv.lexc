

!!!!!! Derivational prefixes

LEXICON V-DERIV-PREFIXES

%<approx%>:м%{Æ%}ч%{ы%}%> Verbs ;
%<ints%>:т%{Æ%}%{N%}%{ы%}%> V-DERIV-PREFIXES-T ;
%<ints2%>:ԓ%{ы%}ги%> V-DERIV-PREFIXES-T ;

[%+desid%]DES%-:р%{Æ%}%> V-DERIV-PREFIXES-T ;
%<ap%>:ин%{Æ%}%> V-DERIV-PREFIXES-T ;
TR%-:%{R%}%{ы%}%> V-DERIV-PREFIXES-T ;
MUT%-:пыԓ%{ы%}%> V-DERIV-PREFIXES-T ;
MUT%-:пыс%{ы%}%> V-DERIV-PREFIXES-T ;
FIN%-:пԓ%{ы%}%> V-DERIV-PREFIXES-T ;
: V-DERIV-PREFIXES-FIN ;

LEXICON V-DERIV-PREFIXES-T

%<ints%>:т%{Æ%}%{N%}%{ы%}%> V-DERIV-PREFIXES-TH ;
%<ints2%>:ԓ%{ы%}ги%> V-DERIV-PREFIXES-TH ;
[%+desid%]DES%-:р%{Æ%}%> V-DERIV-PREFIXES-TH ;
%<ap%>:ин%{Æ%}%> V-DERIV-PREFIXES-TH ;
TR%-:%{R%}%{ы%}%> V-DERIV-PREFIXES-TH ;
: V-DERIV-PREFIXES-TH ;

LEXICON V-DERIV-PREFIXES-TH

! Verb bases
@P.VPR.vb_com@%<%>:@P.VPR.vb_com@г%{Æ%}%> V-DERIV-PREFIXES-FIN ;
@P.VPR.vb_restr@%<%>:@P.VPR.vb_restr@%{Æ%}м%{ы%}%> V-DERIV-PREFIXES-FIN ;
@P.VPR.vb_intens@%<%>:@P.VPR.vb_intens@т%{Æ%}ԓв%{Æ%}%> V-DERIV-PREFIXES-FIN ;
@P.VPR.vb_approx@%<%>:@P.VPR.vb_approx@м%{Æ%}ч%{ы%}%> V-DERIV-PREFIXES-FIN ;
@P.VPR.vb_neg@NEG.PFV%-:@P.VPR.vb_neg@ԓюн%{ы%}%> V-DERIV-PREFIXES-FIN ;
@P.VPR.vb_neg2@CAR%-:@P.VPR.vb_neg2@%{Æ%}%> V-DERIV-PREFIXES-FIN ;
@R.VPR.imposs@%<%>:@R.VPR.imposs@%{Æ%}ʼӄ%{Æ%}%> V-DERIV-PREFIXES-FIN ;
%<ints%>:т%{Æ%}%{N%}%{ы%}%> V-DERIV-PREFIXES-FIN ;
%<ints2%>:ԓ%{ы%}ги%> V-DERIV-PREFIXES-FIN ;
[%+desid%]DES%-:р%{Æ%}%> V-DERIV-PREFIXES-FIN ;
%<ap%>:ин%{Æ%}%> V-DERIV-PREFIXES-FIN ;
TR%-:%{R%}%{ы%}%> V-DERIV-PREFIXES-FIN ;
: V-DERIV-PREFIXES-FIN ;


LEXICON V-DERIV-PREFIXES-FIN
: Verbs ;
: Adjectives ;
: Nouns ;

! %<root here%>


!!!!!! Derivational suffixes
!
LEXICON V-DERIV
! prefix tags
! %<approx%>%[%+approx%]: V-IV-AFFIX ;
! ! thematic smth
%<th%>:%>въи # ;

! incompatible w/others (probably)
%<punct%>:%>%{ы%}чӄыч%{Æ%}т V-DERIV-FIN ;
%<dim%>:%>%{ы%}ӄ%{Æ%}%{Æ%}т V-DERIV-FIN ;
%<aug%>:%>%{ы%}чгат V-DERIV-FIN ;
! attach right to the stem
%-RES:%>%{ы%}тва V-DERIV-TH ;
%-REV:%>%{ы%}тв V-DERIV-TH ;
%-TRSF:%>%{ы%}тви V-DERIV-TH ;
%<purp%>:%>%{ы%}чӄив V-DERIV-TH ;
%-EAT:%>у V-DERIV-TH ;
%<utilit%>:%>%{ы%}тку V-DERIV-TH ;
%-SEARCH:%>%{ы%}гиԓи V-DERIV-TH ;
%-DRAG.OUT:%>%{ы%}гыркы V-DERIV-TH ;
%-GO.TO:%>%{ы%}гт%{⁺ⱽᴴ%} V-DERIV-TH ;
%-VERS:%>ё V-DERIV-TH ;
%-GET:%>%{ы%}нӈ%{Æ%} V-DERIV-TH ;
%-FUND:%>%{ы%}нт V-DERIV-TH ;
%-GO.DO:%>%{ы%}ӈыта V-DERIV-TH ;
%-GO.DO:%>%{ы%}нта V-DERIV-TH ;
%-CATCH:%>%{ы%}ӈытт V-DERIV-TH ;
%-MULT:%>%{ы%}са V-DERIV-TH ;
%-PLUR:%>%{ы%}сир V-DERIV-TH ;
%-COMB:%>%{ы%}сит V-DERIV-TH ;
%-FREQ:%>%{ы%}сыʼт V-DERIV-TH ;
%-MCP:%>%{ы%}сӄив V-DERIV-TH ;
%-RAPID:%>%{ы%}сӄыс%{Æ%}т V-DERIV-TH ;
%-GRAD:%>%{ы%}тыԓ%{Æ%} V-DERIV-TH ;
%-LIBER:%>%{ы%}тур%{Æ%} V-DERIV-TH ;
%-REC:%>%{ы%}выԓг V-DERIV-TH ;
: V-DERIV-TH ;

LEXICON V-DERIV-TH
%-VB:%>%{Æ%}т V-DERIV-TH-T ;
%-CS:%>%{Æ%}в V-DERIV-TH-T ;
%<th%>:%>%{Æ%}%{Æ%} V-DERIV-TH-T ;
%<th%>:%>ъ%{Æ%} V-DERIV-TH-T ;
%-ITER:%>%{ы%}тку V-DERIV-TH-T ;
%-DES%[%+desid%]:%>ӈ V-DERIV-TH-T ;
%-ITER:%>%{ы%}тку V-DERIV-TH-T ;
%-DISTR.S:%>%{ы%}ръу V-DERIV-TH-T ;
%-DISTR.O:%>%{ы%}йв V-DERIV-TH-T ;
%-DISTR.O:%>ив V-DERIV-TH-T ;
%-PLAC:%>%{ы%}ԓъ%{Æ%}т V-DERIV-TH-T ;
%-INCH:%>%{ы%}ӈӈо V-DERIV-TH-T ;
%-INCH:%>%{ы%}ӈоо V-DERIV-TH-T ;
%-INCH:%>%{ы%}мго V-DERIV-TH-T ;
%-COMPL:%>%{ы%}пԓытку V-DERIV-TH-T ;
: V-DERIV-FIN ;


LEXICON V-DERIV-TH-T
%-VB:%>%{Æ%}т V-DERIV-TH-TH ;
%-CS:%>%{Æ%}в V-DERIV-TH-TH ;
%<th%>:%>%{Æ%}%{Æ%} V-DERIV-TH-TH ;
%<th%>:%>ъ%{Æ%} V-DERIV-TH-TH ;
%-ITER:%>%{ы%}тку V-DERIV-TH-TH ;
%-DES%[%+desid%]:%>ӈ V-DERIV-TH-TH ;
%-ITER:%>%{ы%}тку V-DERIV-TH-TH ;
%-DISTR.S:%>%{ы%}ръу V-DERIV-TH-TH ;
%-DISTR.O:%>%{ы%}йв V-DERIV-TH-TH ;
%-DISTR.O:%>ив V-DERIV-TH-TH ;
%-PLAC:%>%{ы%}ԓъ%{Æ%}т V-DERIV-TH-TH ;
%-INCH:%>%{ы%}ӈӈо V-DERIV-TH-TH ;
%-INCH:%>%{ы%}ӈоо V-DERIV-TH-TH ;
%-INCH:%>%{ы%}мго V-DERIV-TH-TH ;
%-COMPL:%>%{ы%}пԓытку V-DERIV-TH-TH ;
: V-DERIV-FIN ;


LEXICON V-DERIV-TH-TH

%-VB:%>%{Æ%}т V-DERIV-FIN ;
%-CS:%>%{Æ%}в V-DERIV-FIN ;
%<th%>:%>%{Æ%}%{Æ%} V-DERIV-FIN ;
%<th%>:%>ъ%{Æ%} V-DERIV-FIN ;
%-ITER:%>%{ы%}тку V-DERIV-FIN ;
%-DES%[%+desid%]:%>ӈ V-DERIV-FIN ;
%-ITER:%>%{ы%}тку V-DERIV-FIN ;
%-DISTR.S:%>%{ы%}ръу V-DERIV-FIN ;
%-DISTR.O:%>%{ы%}йв V-DERIV-FIN ;
%-DISTR.O:%>ив V-DERIV-FIN ;
%-PLAC:%>%{ы%}ԓъ%{Æ%}т V-DERIV-FIN ;
%-INCH:%>%{ы%}ӈӈо V-DERIV-FIN ;
%-INCH:%>%{ы%}ӈоо V-DERIV-FIN ;
%-INCH:%>%{ы%}мго V-DERIV-FIN ;
%-COMPL:%>%{ы%}пԓытку V-DERIV-FIN ;
: V-DERIV-FIN ;


LEXICON V-DERIV-FIN
! these form nouns from verbs
%<ptcp%>: V-DERIV-PTCP ;
@D.VPR@%<ger%>:@D.VPR@%>%{ы%}ԓӄыԓ N-Ia-AATGYR ;
@D.VPR@%-NMLZ:@D.VPR@%>%{ы%}гырг%{⁺ⱽᴴ%} N-III-AVYNRAL ;
@D.VPR@%<ger%>:@D.VPR@%>%{ы%}н%{⁺ⱽᴴ%} N-Vv-AVEEN ;       ! locative nom
@D.VPR@%<ger%>:@D.VPR@%>%{ы%}я N-III-AVYNRAL ;       ! 'age' nominalisation
@D.VPR@%<ger%>:@D.VPR@%>%{и%}н%{Æ%}ӈ%{Æ%} N-Ic-AJKOL ;       ! instrumental nominalisation
@D.VPR@%<ger%>:@D.VPR@%>%{ы%}ёԓг N-III-AVYNRAL ;       ! 'container' nominalisation
@D.VPR@%<ger%>:@D.VPR@ POSS ;       ! possessive nominalisation
@D.VPR@%<ger%>:@D.VPR@ REL ;       ! relational nominalisation
! ! these are verb bases
@R.VPR.vb_com@%<com%>%<vb%>:@R.VPR.vb_com@%>%{T%}%{Æ%} # ;
@R.VPR.vb_com@%<com%>%<vb%>:@R.VPR.vb_com@%>%{ы%}ма # ;
@R.VPR.vb_restr@%<restr%>%<vb%>:@R.VPR.vb_restr@%>%{T%}%{Æ%} # ;
@R.VPR.vb_intens@%<ints%>%<vb%>:@R.VPR.vb_intens@%>%{T%}%{Æ%} # ;
@R.VPR.vb_approx@%<approx%>%<vb%>:@R.VPR.vb_approx@%>%{T%}%{Æ%} # ;
@R.VPR.vb_neg@%-INS:@R.VPR.vb_neg@%>%{T%}%{Æ%} # ;
@R.VPR.vb_neg2@%-CAR%-INS:@R.VPR.vb_neg2@%>%{ы%}к%{Æ%} # ;
@D.VPR@%<vb%>:@D.VPR@%>%{T%}%{Æ%} # ;
@D.VPR@%<vb%>:@D.VPR@%>%{G%}ты%{⁺ⱽᴴ%} # ;
@D.VPR@%<vb%>:@D.VPR@%>%{ы%}ӈ%{⁺ⱽᴴ%} # ;
@R.VPR.imposs@%<imposs%>%<vb%>:@R.VPR.imposs@%>%{ы%}ӈ%{⁺ⱽᴴ%} # ;
! normal verbs
@U.V.iv@:@U.V.iv@ V-IV-AFFIX ;
@U.V.tv@:@U.V.tv@ V-TV-AFFIX ;



!
LEXICON V-DERIV-PTCP
! active (intr only)
!! positive polarity
@U.V.iv@@D.VPR@%<act%>:@U.V.iv@@D.VPR@%>%{ы%}ԓь N-III-AVYNRAL ; ! INTO-N-III ;
!! negative polarity
@U.V.iv@@R.VPR.vb_neg@%-INS:@U.V.iv@@R.VPR.vb_neg@%>%{T%}%{Æ%}%>%{ы%}ԓь N-Ia-AATGYR ; ! INTO-N-I ;
@U.V.iv@@R.VPR.vb_neg2@%-CAR%-INS:@U.V.iv@@R.VPR.vb_neg2@%>%{ы%}к%{Æ%}%>%{ы%}ԓь INTO-N-NEG-PART ;
! passive (tr only)
!! negative polarity
@U.V.tv@@R.VPR.vb_neg@%<pass%>%<neg%>:@U.V.tv@@R.VPR.vb_neg@%>%{ы%}ԓь%>%{T%}%{Æ%} N-Ia-AATGYR ; ! INTO-N-I ;
@U.V.tv@@R.VPR.vb_neg2@%<pass%>%<neg%>:@U.V.tv@@R.VPR.vb_neg2@%>%{ы%}ԓь%>%{Æ%} INTO-N-NEG-PART ;
!! positive polarity
@U.V.tv@@D.VPR@%<pass%>:@U.V.tv@@D.VPR@%>%{ы%}ё%{T%} N-Ia-AATGYR ; ! INTO-N-I ;
