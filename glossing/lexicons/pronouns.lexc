

LEXICON PRON-GYM

@D.CASE@%.prn%.1SG%.ABS:@D.CASE@о # ;
@D.CASE@%.prn%.1SG%.ABS:@D.CASE@ # ;
! .prn%<pers%>%<p1%>%<sg%>%<loc%>:ык # ;
! .prn%<pers%>%<p1%>%<sg%>%<dat%>:ыкы # ;
! .prn%<pers%>%<p1%>%<sg%>%<all%>:ыкагты # ;
! .prn%<pers%>%<p1%>%<sg%>%<equ%>:ыгмил # ;
! .prn%<pers%>%<p1%>%<sg%>%<der%>%<subst%>:%>нин N-DERIV-SUFF ; ! SPATIAL
.prn%.1SG:%>нин N-DERIV-SUFF ; ! SPATIAL
.prn%.1SG: PRON-PERS-CASES ;
@D.CASE@.prn%.1SG:@D.CASE@н POSS ;

LEXICON PRON-GYT

@D.CASE@%.prn%.2SG%.ABS:@D.CASE@то # ;
@D.CASE@%.prn%.2SG%.ABS:@D.CASE@т # ;
! .prn%<pers%>%<p2%>%<sg%>%<erg%>:тнан # ;
! .prn%<pers%>%<p2%>%<sg%>%<loc%>:тык # ;
! .prn%<pers%>%<p2%>%<sg%>%<dat%>:тыкы # ;
! .prn%<pers%>%<p2%>%<sg%>%<all%>:тыкагты # ;
! .prn%<pers%>%<p2%>%<sg%>%<equ%>:тыгмил # ;
.prn%.2SG:%>нин N-DERIV-SUFF ; ! SPATIAL
.prn%.2SG:н PRON-PERS-CASES ;

LEXICON PRON-YTLJON

@D.CASE@.prn%.3SG%.ABS:@D.CASE@тԓён # ;
! .prn%<pers%>%<p3%>%<sg%>%<loc%>:нык # ;
! .prn%<pers%>%<p3%>%<sg%>%<abl%>:ныкагты # ;
@D.CASE@%-ABL:@D.CASE@%>нка%>%{G%}%{ы%}п%{ы%}%{⁺ⱽᴴ%} # ;
@D.CASE@%-ORI:@D.CASE@%>нк%{Æ%}%>%{ы%}гйит # ;
.prn%.3SG:н PRON-PERS-CASES ;

LEXICON PRON-MURI
@D.CASE@.prn%.1PL%.ABS:@D.CASE@и # ;
.prn%.1PL: PRON-PERS-CASES ;

LEXICON PRON-TURI
@D.CASE@.prn%.2PL%.ABS:@D.CASE@и # ;
.prn%.2PL: PRON-PERS-CASES ;

LEXICON PRON-YCCI-ABS
@D.CASE@.prn%.3PL%.ABS:@D.CASE@и # ;

LEXICON PRON-YCCI-OBL
.prn%.3PL: PRON-PERS-CASES ;


LEXICON PRON-PERS-CASES
@D.CASE@%-ERG:@D.CASE@%>нан # ;
@D.CASE@%-ERG:@D.CASE@%>ан # ;
@D.CASE@%-ESS:@D.CASE@%>%{ы%}ку # ;
@D.CASE@%-LOC:@D.CASE@%>%{ы%}к # ;
@D.CASE@%-ALL:@D.CASE@%>ыкы%{⁺ⱽᴴ%} # ;
@D.CASE@%-ALL:@D.CASE@%>ыка%>%{G%}ты%{⁺ⱽᴴ%} # ;
@D.CASE@%-ABL:@D.CASE@%>ыка%>%{G%}%{ы%}п%{ы%}%{⁺ⱽᴴ%} # ;
@D.CASE@%-ORI:@D.CASE@%>ык%{Æ%}%>%{ы%}гйит # ;
@D.CASE@%-INE:@D.CASE@%>ык%{Æ%}%>%{ы%}чыку # ;
@D.CASE@%-PER:@D.CASE@%>ык%{Æ%}%>%{ы%}йэквэ%{⁺ⱽᴴ%} # ;
@D.CASE@%-SUB:@D.CASE@%>ык%{Æ%}%>%{ы%}йиӈкы # ;
@R.CASE.com@%-COM:@R.CASE.com@%>ык%>%{T%}%{Æ%} # ;
@R.CASE.assoc@%-ASSOC:@R.CASE.assoc@%>ыг%>%{ы%}ма%{⁺ⱽᴴ%} # ;
@R.CASE.prv@%-PRV:@R.CASE.prv@%>ык%>%{ы%}к%{Æ%} # ;
@D.CASE@:@D.CASE@ POSS ;
@D.CASE@:@D.CASE@%{ы%}к%{Æ%} REL ;
: PRON-REL ;


LEXICON PRON-REL
%-1SG%-REL:нин%{Æ%}%>кин N-DERIV-I ;
%-1PL%-REL:гин%{Æ%}%>кин N-DERIV-I ;
%-2SG%-REL:ин%{Æ%}%>кин N-DERIV-I ;
%-2PL%-REL:гин%{Æ%}%>кин N-DERIV-I ;



LEXICON PRON-RXENUT-ABS

@D.CASE@%.prn%.SG%.ABS:@D.CASE@%{·%} # ;

LEXICON PRON-RXENUT-OBL
! %<itg%>
.prn: CASES-OBL ;
.prn: CASES-ANIM ;


LEXICON PRON-IMYRXENUT-ABS
! %<ind%>
@D.CASE@%.prn%.SG%.ABS:@D.CASE@%{·%} # ;

LEXICON PRON-IMYRXENUT-OBL

.prn: CASES-OBL ;
.prn: CASES-ANIM ;

LEXICON PRON-YNQEN
! %<dem%>
@D.CASE@%.prn%.SG%.ABS:@D.CASE@%{·%} # ;
.prn: CASES-OBL ;
.prn: CASES-ANIM ;
.prn: POSS ;


LEXICON PRON-YMYLJO
! %<quant%>
.prn%.SG%.ABS: # ;
.prn: CASES-ANIM ;
.prn: CASES-OBL ;
.prn: POSS ;

LEXICON PRON-QOL-SG
! quant
.prn%.SG%.ABS: # ;

LEXICON PRON-QOL ! p.173 Inenlikei

.prn: CASES-OBL ;


LEXICON PRON-CINIT
! %<ref%>
.prn%.SG%.ABS: # ;
.prn: CASES-OBL ;


LEXICON PRON-AMYNAN
! %<restr%>
@D.CASE@.prn%.1SG:@D.CASE@гымнан # ;
@D.CASE@.prn%.2SG:@D.CASE@гынан # ;
@D.CASE@.prn%.3SG:@D.CASE@ынан # ;
@D.CASE@.prn%.1PL:@D.CASE@моргынан # ;
@D.CASE@.prn%.2PL:@D.CASE@торгынан # ;
@D.CASE@.prn%.3PL:@D.CASE@ыргынан # ;
@D.CASE@.prn%.3PL:@D.CASE@ыччынан # ;


LEXICON PRON-ITG
! <itg%>
@D.CASE@.prn%:@D.CASE@ # ;

! LEXICON PRON                                 !! Generic pronoun [to be categorised]

! .prn: # ;






LEXICON Pronouns

гым:гым PRON-GYM ;
гыт:гы PRON-GYT ;
гыт:гыы PRON-GYT ;  ! Dir/LR
ытԓён:ы PRON-YTLJON ;
мури:мур PRON-MURI ;  ! 1pl
тури:тур PRON-TURI ;  ! 2pl
ыччи:ычч PRON-YCCI-ABS ;     ! 3pl females
ыччи:ычч PRON-YCCI-OBL ;     ! 3pl females
ытри:ытр PRON-YCCI-ABS ;     ! 3pl males
ытри:ырг PRON-YCCI-OBL ;     ! 3pl males


ымыԓьо:ымыԓьо PRON-YMYLJO ;

ӄоԓ:ӄоԓ PRON-QOL-SG ;
ӄоԓ:ӄут PRON-QOL ;


чиниткин:чиниткин PRON-CINIT ; ! "свой"


ӈотӄэн:ӈотӄэна PRON-YNQEN ;
ӈанӄэн:ӈанӄэна PRON-YNQEN ;
ӈаанӄэн:ӈаанӄэна PRON-YNQEN ;
ӈоонӄэн:ӈоонӄэна PRON-YNQEN ;
ынӄэн:ынӄэна PRON-YNQEN ;

мэӈин:мэӈин%{Æ%} PRON-RXENUT-ABS ;     ! кто
мэӈин:мик PRON-RXENUT-OBL ;   ! кто
ръэнут:ръэнут%{Æ%} PRON-RXENUT-ABS ;     ! что
ръэнут:рэӄ PRON-RXENUT-OBL ;  ! что

имыръэнут:имыръэнут%{Æ%} PRON-IMYRXENUT-ABS ;   ! всё
имыръэнут:имырэӄ PRON-IMYRXENUT-OBL ;  ! всё
имымэӈин:имымэӈин%{Æ%} PRON-IMYRXENUT-ABS ;     ! все
имымэӈин:имымик PRON-IMYRXENUT-OBL ;   ! все

ам:ам PRON-AMYNAN ;

эʼми:эʼми PRON-ITG ;    ! где (с существитеԓьными)
