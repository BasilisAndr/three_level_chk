

!!!!!! Derivational prefixes

LEXICON V-DERIV-PREFIXES
: V-DERIV-PREFIXES-F ;
: V-DERIV-PREFIXES-T ;
: V-DERIV-PREFIXES-TH ;

LEXICON V-DERIV-PREFIXES-F

APPROX%-:м%{Æ%}ч%{ы%}%> Verbs "weight: 0.5" ;
INTS%-:т%{Æ%}%{N%}%{ы%}%> V-DERIV-PREFIXES-T "weight: 0.5" ;
INTS%-:ԓ%{ы%}ги%> V-DERIV-PREFIXES-T "weight: 0.5" ;

[%+desid%]DES%-:р%{Æ%}%> V-DERIV-PREFIXES-T "weight: 0.5" ;
AP%-:ин%{Æ%}%> V-DERIV-PREFIXES-T "weight: 0.5" ;
TR%-:%{R%}%{ы%}%> V-DERIV-PREFIXES-T "weight: 0.5" ;
MUT%-:пыԓ%{ы%}%> V-DERIV-PREFIXES-T "weight: 0.5" ;
MUT%-:пыч%{ы%}%> V-DERIV-PREFIXES-T "weight: 0.5" ;
FIN%-:пԓ%{ы%}%> V-DERIV-PREFIXES-T "weight: 0.5" ;

LEXICON V-DERIV-PREFIXES-T

%<ints%>:т%{Æ%}%{N%}%{ы%}%> V-DERIV-PREFIXES-TH "weight: 0.5" ;
%<ints2%>:ԓ%{ы%}ги%> V-DERIV-PREFIXES-TH "weight: 0.5" ;
[%+desid%]DES%-:р%{Æ%}%> V-DERIV-PREFIXES-TH "weight: 0.5" ;
AP%-:ин%{Æ%}%> V-DERIV-PREFIXES-TH "weight: 0.5" ;
TR%-:%{R%}%{ы%}%> V-DERIV-PREFIXES-TH "weight: 0.5" ;
: V-DERIV-PREFIXES-FIN "weight: 0.5" ;

LEXICON V-DERIV-PREFIXES-TH

! Verb bases
@D.VPR@@P.VPR.vb-com@%COM%-:@D.VPR@@P.VPR.vb-com@г%{Æ%}%> V-DERIV-PREFIXES-FIN "weight: 0.5" ;
@D.VPR@@P.VPR.vb-restr@RESTR%-:@D.VPR@@P.VPR.vb-restr@%{Æ%}м%{ы%}%> V-DERIV-PREFIXES-FIN "weight: 0.5" ;
@D.VPR@@P.VPR.vb-intens@%-INTS:@D.VPR@@P.VPR.vb-intens@т%{Æ%}ԓв%{Æ%}%> V-DERIV-PREFIXES-FIN "weight: 0.5" ;
@D.VPR@@P.VPR.vb-approx@APPROX%-:@D.VPR@@P.VPR.vb-approx@м%{Æ%}ч%{ы%}%> V-DERIV-PREFIXES-FIN "weight: 0.5" ;
@D.VPR@@P.VPR.vb-neg@NEG.PFV%-:@D.VPR@@P.VPR.vb-neg@ԓюн%{ы%}%> V-DERIV-PREFIXES-FIN "weight: 0.5" ;
@D.VPR@@P.VPR.vb-neg2@CAR%-:@D.VPR@@P.VPR.vb-neg2@%{Æ%}%> V-DERIV-PREFIXES-FIN "weight: 0.5" ;
@D.VPR@@P.VPR.vb-neg2@PREF%-CAR%-:@D.VPR@@P.VPR.vb-neg2@%{Æ%}вн%>%{Æ%}%> V-DERIV-PREFIXES-FIN "weight: 0.5" ;
@D.VPR@@P.VPR.imposs@%<imposs%>:@D.VPR@@P.VPR.imposs@%{ʔ%}%{Æ%}ʼӄ%{Æ%}%> V-DERIV-PREFIXES-FIN "weight: 0.5" ;
INTS%-:т%{Æ%}%{N%}%{ы%}%> V-DERIV-PREFIXES-FIN "weight: 0.5" ;
INTS%-:ԓ%{ы%}ги%> V-DERIV-PREFIXES-FIN "weight: 0.5" ;
[%+desid%]DES%-:р%{Æ%}%> V-DERIV-PREFIXES-FIN "weight: 0.5" ;
AP%-:ин%{Æ%}%> V-DERIV-PREFIXES-FIN "weight: 0.5" ;
TR%-:%{R%}%{ы%}%> V-DERIV-PREFIXES-FIN "weight: 0.5" ;
: V-DERIV-PREFIXES-FIN ;


LEXICON V-DERIV-PREFIXES-FIN
: Verbs ;
! : Adjectives ;
: Nouns ;

! %<root here%>


!!!!!! Derivational suffixes
!
LEXICON V-DERIV
: V-DERIV-F ;
: V-DERIV-TH ;
: V-DERIV-TH-T ;
: V-DERIV-TH-TH ;


LEXICON V-DERIV-F
! prefix tags
! %<approx%>%[%+approx%]: V-IV-AFFIX ;
! ! thematic smth
@D.VPR@%-TH:@D.VPR@%>въи # ;

! incompatible w/others (probably)
%-PUNCT:%>%{ы%}чӄыч%{Æ%}т V-DERIV-FIN "weight: 0.5" ;
%-DIM:%>%{ы%}ӄ%{Æ%}%{Æ%}т V-DERIV-FIN "weight: 0.5" ;
%-AUG:%>%{ы%}чгат V-DERIV-FIN "weight: 0.5" ;
! attach right to the stem
%-RES:%>%{ы%}тва V-DERIV-TH "weight: 0.5" ;
%-REV:%>%{ы%}тв V-DERIV-TH "weight: 0.5" ;
%-TRSF:%>%{ы%}тви V-DERIV-TH "weight: 0.5" ;
%-PURP:%>%{ы%}чӄив V-DERIV-TH "weight: 0.5" ;
%-EAT:%>у V-DERIV-TH "weight: 0.5" ;
%<utilit%>:%>%{ы%}тку V-DERIV-TH "weight: 0.5" ;
%-SEARCH:%>%{ы%}гиԓи V-DERIV-TH "weight: 0.5" ;
%-DRAG.OUT:%>%{ы%}гыркы V-DERIV-TH "weight: 0.5" ;
%-GO.TO:%>%{ы%}гт%{⁺ⱽᴴ%} V-DERIV-TH "weight: 0.5" ;
%-VERS:%>ё V-DERIV-TH "weight: 0.5" ;
%-GET:%>%{ы%}нӈ%{Æ%} V-DERIV-TH "weight: 0.5" ;
%-FUND:%>%{ы%}нт V-DERIV-TH "weight: 0.5" ;
%-GO.DO:%>%{ы%}ӈыта V-DERIV-TH "weight: 0.5" ;
%-GO.DO:%>%{ы%}нта V-DERIV-TH "weight: 0.5" ;
%-CATCH:%>%{ы%}ӈытт V-DERIV-TH "weight: 0.5" ;
%-MULT:%>%{ы%}ча V-DERIV-TH "weight: 0.5" ;
%-PLUR:%>%{ы%}чир V-DERIV-TH "weight: 0.5" ;
%-COMB:%>%{ы%}чит V-DERIV-TH "weight: 0.5" ;
%-FREQ:%>%{ы%}чыʼт V-DERIV-TH "weight: 0.5" ;
%-MCP:%>%{ы%}чӄив V-DERIV-TH "weight: 0.5" ;
%-RAPID:%>%{ы%}чӄыч%{Æ%}т V-DERIV-TH "weight: 0.5" ;
%-GRAD:%>%{ы%}тыԓ%{Æ%} V-DERIV-TH "weight: 0.5" ;
%-LIBER:%>%{ы%}тур%{Æ%} V-DERIV-TH "weight: 0.5" ;
%-REC:%>%{ы%}выԓг V-DERIV-TH "weight: 0.5" ;
: V-DERIV-FIN ;

LEXICON V-DERIV-TH
%-VB:%>%{Æ%}т V-DERIV-TH-T "weight: 0.5" ;
%-CS:%>%{Æ%}в V-DERIV-TH-T "weight: 0.5" ;
%-TH:%>%{Æ%}%{Æ%} V-DERIV-TH-T "weight: 0.5" ;
%-TH:%>ъ%{Æ%} V-DERIV-TH-T "weight: 0.5" ;
%-ITER:%>%{ы%}тку V-DERIV-TH-T "weight: 0.5" ;
%-DES%[%+desid%]:%>ӈ V-DERIV-TH-T "weight: 0.5" ;
%-ITER:%>%{ы%}тку V-DERIV-TH-T "weight: 0.5" ;
%-DISTR%.S:%>%{ы%}ръу V-DERIV-TH-T "weight: 0.5" ;
%-DISTR%.O:%>%{ы%}йв V-DERIV-TH-T "weight: 0.5" ;
%-DISTR%.O:%>ив V-DERIV-TH-T "weight: 0.5" ;
%-PLAC:%>%{ы%}ԓъ%{Æ%}т V-DERIV-TH-T "weight: 0.5" ;
%-INCH:%>%{ы%}ӈӈо V-DERIV-TH-T "weight: 0.5" ;
%-INCH:%>%{ы%}ӈоо V-DERIV-TH-T "weight: 0.5" ;
%-INCH:%>%{ы%}мго V-DERIV-TH-T "weight: 0.5" ;
%-COMPL:%>%{ы%}пԓытку V-DERIV-TH-T "weight: 0.5" ;
: V-DERIV-FIN ;


LEXICON V-DERIV-TH-T
%-VB:%>%{Æ%}т V-DERIV-TH-TH "weight: 0.5" ;
%-CS:%>%{Æ%}в V-DERIV-TH-TH "weight: 0.5" ;
%-TH:%>%{Æ%}%{Æ%} V-DERIV-TH-TH "weight: 0.5" ;
%-TH:%>ъ%{Æ%} V-DERIV-TH-TH "weight: 0.5" ;
%-ITER:%>%{ы%}тку V-DERIV-TH-TH "weight: 0.5" ;
%-DES%[%+desid%]:%>ӈ V-DERIV-TH-TH "weight: 0.5" ;
%-ITER:%>%{ы%}тку V-DERIV-TH-TH "weight: 0.5" ;
%-DISTR.S:%>%{ы%}ръу V-DERIV-TH-TH "weight: 0.5" ;
%-DISTR.O:%>%{ы%}йв V-DERIV-TH-TH "weight: 0.5" ;
%-DISTR.O:%>ив V-DERIV-TH-TH "weight: 0.5" ;
%-PLAC:%>%{ы%}ԓъ%{Æ%}т V-DERIV-TH-TH "weight: 0.5" ;
%-INCH:%>%{ы%}ӈӈо V-DERIV-TH-TH "weight: 0.5" ;
%-INCH:%>%{ы%}ӈоо V-DERIV-TH-TH "weight: 0.5" ;
%-INCH:%>%{ы%}мго V-DERIV-TH-TH "weight: 0.5" ;
%-COMPL:%>%{ы%}пԓытку V-DERIV-TH-TH "weight: 0.5" ;
: V-DERIV-FIN ;


LEXICON V-DERIV-TH-TH

%-VB:%>%{Æ%}т V-DERIV-FIN "weight: 0.5" ;
%-CS:%>%{Æ%}в V-DERIV-FIN "weight: 0.5" ;
%-TH:%>%{Æ%}%{Æ%} V-DERIV-FIN "weight: 0.5" ;
%-TH:%>ъ%{Æ%} V-DERIV-FIN "weight: 0.5" ;
%-ITER:%>%{ы%}тку V-DERIV-FIN "weight: 0.5" ;
%-DES%[%+desid%]:%>ӈ V-DERIV-FIN "weight: 0.5" ;
%-ITER:%>%{ы%}тку V-DERIV-FIN "weight: 0.5" ;
%-DISTR.S:%>%{ы%}ръу V-DERIV-FIN "weight: 0.5" ;
%-DISTR.O:%>%{ы%}йв V-DERIV-FIN "weight: 0.5" ;
%-DISTR.O:%>ив V-DERIV-FIN "weight: 0.5" ;
%-PLAC:%>%{ы%}ԓъ%{Æ%}т V-DERIV-FIN "weight: 0.5" ;
%-INCH:%>%{ы%}ӈӈо V-DERIV-FIN "weight: 0.5" ;
%-INCH:%>%{ы%}ӈоо V-DERIV-FIN "weight: 0.5" ;
%-INCH:%>%{ы%}мго V-DERIV-FIN "weight: 0.5" ;
%-COMPL:%>%{ы%}пԓытку V-DERIV-FIN "weight: 0.5" ;
: V-DERIV-FIN ;


LEXICON V-DERIV-FIN
! these form nouns from verbs
%.ptcp: V-DERIV-PTCP ;
@D.VPR@%<ger%>:@D.VPR@%>%{ы%}ԓӄыԓ N-Ia-AATGYR ;
@D.VPR@%-NMLZ:@D.VPR@%>%{ы%}гырг%{⁺ⱽᴴ%} N-III-AVYNRAL ;
@D.VPR@%<ger%>:@D.VPR@%>%{ы%}н%{⁺ⱽᴴ%} N-Vv-AVEEN ;       ! locative nom
@D.VPR@%<ger%>:@D.VPR@%>%{ы%}я N-III-AVYNRAL ;       ! 'age' nominalisation
@D.VPR@%<ger%>:@D.VPR@%>%{и%}н%{Æ%}ӈ%{Æ%} N-Ic-AJKOL ;       ! instrumental nominalisation
@D.VPR@%<ger%>:@D.VPR@%>%{ы%}ёԓг N-III-AVYNRAL ;       ! 'container' nominalisation
@D.VPR@%<ger%>:@D.VPR@ POSS ;       ! possessive nominalisation
@D.VPR@%<ger%>:@D.VPR@ REL ;       ! relational nominalisation
! ! these are verb bases
@R.VPR.vb-com@%<com%>%<vb%>:@R.VPR.vb-com@%>%{T%}%{Æ%} # ;
@R.VPR.vb-com@%<com%>%<vb%>:@R.VPR.vb-com@%>%{ы%}ма # ;
@R.VPR.vb-restr@%<restr%>%<vb%>:@R.VPR.vb-restr@%>%{T%}%{Æ%} # ;
@R.VPR.vb-intens@%-INTS.vb:@R.VPR.vb-intens@%>%{T%}%{Æ%} # ;
@R.VPR.vb-approx@%<approx%>%<vb%>:@R.VPR.vb-approx@%>%{T%}%{Æ%} # ;
@R.VPR.vb-neg@%-INS:@R.VPR.vb-neg@%>%{T%}%{Æ%} # ;
@R.VPR.vb-neg2@%-CAR%-INS:@R.VPR.vb-neg2@%>%{ы%}к%{Æ%} # ;
@D.VPR@%<vb%>:@D.VPR@%>%{T%}%{Æ%} # ;
@D.VPR@%<vb%>:@D.VPR@%>%{G%}ты%{⁺ⱽᴴ%} # ;
@D.VPR@%<vb%>:@D.VPR@%>%{ы%}ӈ%{⁺ⱽᴴ%} # ;
@R.VPR.imposs@%<imposs%>%<vb%>:@R.VPR.imposs@%>%{ы%}ӈ%{⁺ⱽᴴ%} # ;
! normal verbs
@U.V.iv@:@U.V.iv@ V-IV-AFFIX ;
@U.V.tv@:@U.V.tv@ V-TV-AFFIX ;



!
LEXICON V-DERIV-PTCP
! active (intr only)
!! positive polarity
@U.V.iv@%<act%>:@U.V.iv@%>%{ы%}ԓь N-DERIV-III ; ! INTO-N-III ;
!! negative polarity
@U.V.iv@@R.VPR.vb-neg@%-INS%-ATTR:@U.V.iv@@R.VPR.vb-neg@%>%{T%}%{Æ%}%>%{ы%}ԓь INTO-N-NEG-PART ; ! INTO-N-I ;
@U.V.iv@@R.VPR.vb-neg2@%-CAR%-ATTR:@U.V.iv@@R.VPR.vb-neg2@%>%{ы%}к%>%{ы%}ԓь INTO-N-NEG-PART ;
! passive (tr only)
!! negative polarity
@U.V.tv@@R.VPR.vb-neg@%<pass%>%<neg%>:@U.V.tv@@R.VPR.vb-neg@%>%{ы%}ԓь%>%{T%}%{Æ%} INTO-N-NEG-PART ; ! INTO-N-I ;
@U.V.tv@@R.VPR.vb-neg2@%<pass%>%<neg%>:@U.V.tv@@R.VPR.vb-neg2@%>%{ы%}ԓь%>%{Æ%} INTO-N-NEG-PART ;
!! positive polarity
@U.V.tv@%<pass%>:@U.V.tv@%>%{ы%}ё%{T%} INTO-N-NEG-PART ; ! INTO-N-I ;
